##############################################################################
# cmake configuration file for KalTest2
#
# requires:
#   MacroCheckPackageLibs.cmake for checking package libraries
#
# returns following variables:
#
#   KalTest2_FOUND      : set to TRUE if KalTest2 found
#       if FIND_PACKAGE called with REQUIRED and COMPONENTS arguments
#       KalTest2_FOUND is only set to TRUE if ALL components are also found
#       if REQUIRED is NOT set components may or may not be available
#
#   KalTest2_ROOT       : path to this KalTest2 installation
#   KalTest2_VERSION    : package version
#   KalTest2_LIBRARIES  : list of KalTest2 libraries (NOT including COMPONENTS)
#   KalTest2_INCLUDE_DIRS  : list of paths to be used with INCLUDE_DIRECTORIES
#   KalTest2_LIBRARY_DIRS  : list of paths to be used with LINK_DIRECTORIES
#   KalTest2_COMPONENT_LIBRARIES      : list of KalTest2 component libraries
#   KalTest2_${COMPONENT}_FOUND       : set to TRUE or FALSE for each library
#   KalTest2_${COMPONENT}_LIBRARY     : path to individual libraries
#   KalTest2_${COMPONENT}_LIB_DEPENDS : individual library dependencies
#
# @author Jan Engels, Desy
##############################################################################

SET( KalTest2_ROOT "@CMAKE_INSTALL_PREFIX@" )
SET( KalTest2_VERSION "@KalTest2_VERSION@" )


# ---------- include dirs -----------------------------------------------------
# do not store find results in cache
SET( KalTest2_INCLUDE_DIRS KalTest2_INCLUDE_DIRS-NOTFOUND )
MARK_AS_ADVANCED( KalTest2_INCLUDE_DIRS )

FIND_PATH( KalTest2_INCLUDE_DIRS
	NAMES kaltest/TVTrack.h
	PATHS ${KalTest2_ROOT}/include
	NO_DEFAULT_PATH
)

# fix for backwards compatibility
IF( KalTest2_INCLUDE_DIRS )
    LIST( APPEND KalTest2_INCLUDE_DIRS ${KalTest2_INCLUDE_DIRS}/kaltest )
ENDIF( KalTest2_INCLUDE_DIRS )


# ---------- libraries --------------------------------------------------------
INCLUDE( "@ILCSOFT_CMAKE_MODULES_ROOT@/MacroCheckPackageLibs.cmake" )

# only standard libraries should be passed as arguments to CHECK_PACKAGE_LIBS
# additional components are set by cmake in variable PKG_FIND_COMPONENTS
# first argument should be the package name
CHECK_PACKAGE_LIBS( KalTest2 KalTest2 )




# ---------- libraries dependencies -------------------------------------------
# this sets KalTest2_${COMPONENT}_LIB_DEPENDS variables
INCLUDE( "${KalTest2_ROOT}/lib/cmake/KalTest2LibDeps.cmake" )
 



# ---------- final checking ---------------------------------------------------
INCLUDE( FindPackageHandleStandardArgs )
# set KALTEST_FOUND to TRUE if all listed variables are TRUE and not empty
# KalTest2_COMPONENT_VARIABLES will be set if FIND_PACKAGE is called with REQUIRED argument
FIND_PACKAGE_HANDLE_STANDARD_ARGS( KalTest2 DEFAULT_MSG KalTest2_ROOT KalTest2_INCLUDE_DIRS KalTest2_LIBRARIES ${KalTest2_COMPONENT_VARIABLES} )

SET( KalTest2_FOUND ${KALTEST_FOUND} )

